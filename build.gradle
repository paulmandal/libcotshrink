buildscript {
    ext.ATAK_VERSION = "4.10.0"

    ext.takrepoUrl = 'http://localhost/'
    ext.takrepoUser = 'invalid'
    ext.takrepoPassword = 'invalid'
    ext.takdevPlugin = "${rootDir}/../atak-civ-sdk-4.10.1/atak-gradle-takdev.jar"

    ext.isDevKitEnabled = { ->
        return false
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.4"

        classpath files(takdevPlugin)
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

ext {
    PUBLISH_GROUP_ID = 'com.paulmandal.atak'
    PUBLISH_VERSION = '1.0.4'
    PUBLISH_ARTIFACT_ID = 'libcotshrink'
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'atak-takdev-plugin'
apply from: "${rootProject.projectDir}/scripts/publish-root.gradle"
apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            proto {
                srcDir 'src/main/protobuf'
            }
        }
        test {
            proto {
                srcDir 'src/main/protobuf'
            }
            java.srcDirs 'src/test/java'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-javalite:3.21.12'
    implementation 'com.siemens.ct.exi:exificient:1.0.4'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.4'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.9'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

