apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    buildToolsVersion "30.0.1"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {

            proto {
                srcDir 'src/main/protobuf'
                srcDir 'src/main/protocolbuffers'
            }
        }
        test {
            proto {
                srcDir 'src/test/protocolbuffers'
            }
            java.srcDirs 'src/test/java'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
}

def getAtakLibPath() {
    def atakLibPathBase = 'AndroidTacticalAssaultKit-CIV/atak/ATAK/app/build/libs/4.1.0.CIV/main.jar'
    def atakLibPath = "../../${atakLibPathBase}"
    def atakLibFile = new File("${project.projectDir}/${atakLibPath}")

    if (!atakLibFile.exists()) {
        // fallback to building standalone
        atakLibPath = "../${atakLibPathBase}"
        atakLibFile = new File("${project.projectDir}/${atakLibPath}")
    }

    if (!atakLibFile.exists()) {
        throw new Exception("The libcotshrink directory must be either side-by-side in the same parent directory as AndroidTacticalAssaultKit-CIV or it must be inside a project that is side-by-side in the same parent directory as AndroidTacticalAssaultKit-CIV")
    }

    return atakLibPath
}

dependencies {
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'com.siemens.ct.exi:exificient:1.0.4'

    compileOnly fileTree(dir: getAtakLibPath(), include: '*.jar')

    testImplementation 'junit:junit:4.13'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.7.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

buildscript {
    repositories {
        google()
        jcenter()
        maven {
          url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply from: 'install.gradle'
apply from: 'bintray.gradle'
