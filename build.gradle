buildscript {
    ext.ATAK_VERSION = "4.3.0"

    def takdevVersion = '2.0.0'

    ext.takrepoUrl = 'http://localhost/'
    ext.takrepoUser = 'invalid'
    ext.takrepoPassword = 'invalid'
    ext.takdevPlugin = "${rootDir}/../AndroidTacticalAssaultKit-CIV/atak-gradle-takdev/build/libs/atak-gradle-takdev-${takdevVersion}.jar"

    ext.isDevKitEnabled = { ->
        return false
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.14"

        classpath files(takdevPlugin)
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'atak-takdev-plugin'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {

            proto {
                srcDir 'src/main/protobuf'
                srcDir 'src/main/protocolbuffers'
            }
        }
        test {
            proto {
                srcDir 'src/test/protocolbuffers'
            }
            java.srcDirs 'src/test/java'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
}

dependencies {
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'com.siemens.ct.exi:exificient:1.0.4'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.robolectric:robolectric:4.4'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.7.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}
